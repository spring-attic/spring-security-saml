/*
 * Copyright 2002-2019 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 *
 */

apply plugin: "propdeps-maven"

install {
	repositories.mavenInstaller {
		customizePom(pom, project)
	}
}

task generatePom {
	group = 'Build'
	description = 'Generates a Maven pom.xml'

	ext.generatedPomFileName = "pom.xml"
	onlyIf { install.enabled }

	inputs.files(fileTree(project.rootProject.rootDir).include("**/*.gradle").files)
	inputs.files(new File(project.rootProject.rootDir, Project.GRADLE_PROPERTIES))
	outputs.files(generatedPomFileName)

	doLast() {
		def p = pom {}
		customizePom(p, project)
		p.writeTo(generatedPomFileName)
	}
}

build.dependsOn generatePom

def customizePom(pom, gradleProject) {
	pom.whenConfigured { generatedPom ->

		// sort to make pom dependencies order consistent to ease comparison of older poms
		generatedPom.dependencies = generatedPom.dependencies.sort { dep ->
			"$dep.scope:$dep.groupId:$dep.artifactId"
		}

		// add all items necessary for maven central publication
		generatedPom.project {
			name = gradleProject.description
			description = gradleProject.description
			url = "https://github.com/spring-projects/spring-security-saml"
			organization {
				name = "Spring Framework"
				url = "http://projects.spring.io/spring-security-saml/"
			}
			licenses {
				license {
					name "The Apache Software License, Version 2.0"
					url "http://www.apache.org/licenses/LICENSE-2.0.txt"
					distribution "repo"
				}
			}
			scm {
				url = "https://github.com/spring-projects/spring-security-saml"
				connection = "scm:git:git://github.com/spring-projects/spring-security-saml"
				developerConnection = "scm:git:git://github.com/spring-projects/spring-security-saml"
			}
			developers {
				developer {
					id = "fhanik"
					name = "Filip Hanik"
					email = "fhanik@pivotal.io"
					timezone = "-8"
				}
			}
			contributors {
			}
			issueManagement {
				system = "github"
				url = "https://github.com/spring-projects/spring-security-saml/issues"
			}
			mailingLists {
				mailingList {
					name = "Spring Security SAML Forum"
					post = "https://stackoverflow.com/questions/tagged/spring-saml"
					archive = "https://stackoverflow.com/questions/tagged/spring-saml"
				}
			}
		}
	}
}

